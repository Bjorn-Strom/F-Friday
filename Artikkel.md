# F# Friday 3

Hei og velkommen til den tredje posten i en serie om programmeringsspr√•ket F#!

[Forrige gang]() startet vi √• definere typene v√•r lille matoppskrifts-app skal best√• av. Vi lagde ogs√• noen enkle hjelpefunksjoner som lar oss opprette oppskrifter. Denne gangen skal vi putte denne koden inn i en backend, slik at vi kan utf√∏re CRUD operasjoner p√• og med oppskriftene v√•re.

## Dagens agenda¬†üìã

Denne gangen skal vi se p√• hvordan man kan strukturere en server i F#. Her finnes det ganske mange forskjellige biblioteker som alle har sine egne filosofier og egne m√•ter √• gj√∏re ting p√•. Samtidig s√• h√•ndterer de fleste HTTP-requests p√• en ganske s√• lik m√•te. S√• kunnskap er ganske overf√∏rbar mellom disse forskjellige alternativene. Vi skal se p√• en som er ganske popul√¶r: nemlig [Giraffe](https://github.com/giraffe-fsharp/Giraffe), men aller f√∏rst m√• vi en tur innom fugleriket.

## Kestrel üê¶
Dersom man har jobbet litt i Dotnet-verden fra f√∏r kjenner man antageligvis igjen navnet [Kestrel](https://docs.microsoft.com/en-us/aspnet/core/fundamentals/servers/kestrel?view=aspnetcore-5.0). Det er web server implementasjonen som er standard i ASP.NET Core. Giraffe biblioteket er en funksjonell wrapper p√• Kestrel for √• forenkle dens bruk i F#.

Giraffe lar deg konfigurere b√•de app og services som du kanskje kjenner igjen fra C#, samtidig som den lar deg jobbe med routes p√• en enkel m√•te. Den st√∏rste forskjellen er nok at de har kvittet seg med den objekt- og dependency injection orienterte m√•ten √• gj√∏re ting p√•. N√• trenger man kun √• forholde seg til enkle funksjoner og funksjonskomposisjon.

I Giraffe fungerer routing slik at du har funksjoner for hver HTTP metode. Disse funksjonene kan du bruke med funksjonskomposisjon for √• definere hva som skal skje for hver av disse rutene. For eksempel:

```fsharp
let ruter =
    choose
        [ POST >=> choose [ route "/login" >=> loginHandler
                            route "/logout" >=> logoutHandler
                          ]
          GET >=> choose [ route "/ping"  >=> text "pong"
                           route "/userInfo" >=> userInfoHandler
                         ]
        ]
```

Det f√∏rste man nok legger merke til her er den underlige operatoren: >=>. Operatoren er shorthand for funksjonen composeog kan tenkes p√• som en annen m√•te √• komponere sammen funksjoner p√•. Den kalles for en fishbone operator og utf√∏rer kleisli-komposisjon, du kan lese mer i [denne](https://functional.christmas/2019/14) artikkelen, men det er ikke n√∏dvendig for √• f√∏lge med videre her.

I eksempelet over har vi definert 4 ruter i en liste. Vi ser at vi har 2 POST endepunkt hvor man kan velge mellom 2 ruter, login og logout. Dersom ruten matcher en av disse s√• utf√∏res den tilsvarende funksjonen. S√• en POST request til ruten `/login` kaller funksjonen `loginHandler`.

Det er ganske enkelt og det er veldig fort gjort √• sette opp nye ruter. Giraffe har ogs√• noen innebygde funksjoner som gj√∏r det enklere √• returnere tekst eller json direkte. Dette ser vi i `ping` endepunktet.

Dette er ogs√• veldig enkelt √• bygge videre p√•. La oss si at `Get "/userInfo"` ruten trenger autorisering. Da kan du lage en funksjon som tar seg av det og den kobles rett inn i komposisjonen:

```fsharp
route "/userInfo" >=> mustBeLoggedIn >=> userInfoHandler
```

## Giraffe setup¬†ü¶í
Okei nok teori. Learn by doing, I say!

Forh√•pentligvis har du allerede et prosjekt liggende fra forrige artikkel for n√• trenger vi √• hente ned Giraffe nuget pakken. I skrivende stund er nyeste versjon 5.0.0, s√• skriv dette i en terminal i prosjektet ditt:

```fsharp
dotnet add package Giraffe - version 5.0.0
```

Med Giraffe installert s√• kan vi sette opp en helt enkel backend. La oss starte med √• definere en testrute s√• vi kan sjekke at alt funker:
```fsharp
let routes = choose [ route "/ping" >=> text "pong"]
```

Dersom du ikke spesifiserer en spesifikk HTTP metode s√• vil den funke p√• alle.

Vi trenger noen funksjoner for √• sette opp Giraffe ogs√•.

```fsharp
let configureApp (app: IApplicationBuilder) =
    app.UseGiraffe routes
let configureServices (services: IServiceCollection) =
    services.AddGiraffe() |> ignore
```

Disse funksjonene vil tilsvare det du finner i en typisk `startup.cs` fil i et C# backend prosjekt, s√• om du √∏nsker √• legge til CORS eller andre ting er dette stedet √• gj√∏re det p√•. I disse funksjonene har vi sagt hvilke ruter vi √∏nsker √• bruke samt lagt til Giraffe.

Det eneste vi mangler n√• for √• ha en fungerende backend er √• koble alt dette sammen med en `webHostBuilder`. I Giraffe vil de se slik ut:

```fsharp
[<EntryPoint>]
let main args =
    Host.CreateDefaultBuilder(args)
        .ConfigureWebHostDefaults(
            fun webHostBuilder ->
                webHostBuilder
                    .Configure(Action<IApplicationBuilder> configureApp)
                    .ConfigureServices(configureServices)
                    |> ignore)
        .Build()
        .Run()
    0
```

Dette vil knytte alt sammen og initialisere serveren v√•r.

La oss starte serveren v√•r og bruke en REST-klient til √• sjekke endepunktet v√•rt.

![alt text](works.png "Det funker!")

It wooooooorks! Med 30 linjer kode har vi en fungerende backend.

## Ruter ü™ü

La oss sette opp ruter til de forskjellige funksjonene vi √∏nsker √• st√∏tte. I f√∏rste omgang s√• kan vi starte med √•:

- Hente ut alle oppskriftene.
- Lagre nye oppskrifter.
- Oppdatere en oppskrift.
- Slette en oppskrift.

For √• f√• til dette s√• trenger vi disse metodene: `GET`, `POST`, `PUT` og `DELETE`

```fsharp
let routes =
    choose [ GET    >=> route  "/api/recipes"    >=> getRecipes 
             POST   >=> route  "/api/recipe"     >=> postRecipe
             PUT    >=> route  "/api/recipe"     >=> putRecipe
             DELETE >=> routef "/api/recipe/%O" deleteRecipe
             RequestErrors.NOT_FOUND "Not found"
           ]
```

I v√•r delete request sier vi ogs√• at vi forventer en GUID. Dette gj√∏r vi ved √• bruke `routef` funksjonen sammen med `%o` som er Giraffes m√•te √• si at vi forventer en eller annen query parameter her. `%o` er GUID, men det finnes mange [flere](https://github.com/giraffe-fsharp/Giraffe/blob/master/DOCUMENTATION.md#routef).

Her har vi ogs√• lagt inn en default rute, s√• dersom man har en request som ikke treffer noen av de definerte rutene s√• f√•r vi en 404‚Ää-‚ÄäNot Found.

Det vi trenger n√• er √• implementere disse funksjonene som rutene bruker.

## Database? üìí
Vanligvis n√•r man har en backend har man ogs√• en database som lagrer data. Det skal vi ogs√• gj√∏re, men ikke denne gangen. Til √• starte med skal vi gj√∏re det veldig enkelt og heller bruke en klasse og en C# dictionary til √• lagre oppskriftene v√•re. Det betyr dessverre at dataen ikke blir persistert, men det er noe vi kan fikse senere. Implementasjonen av denne databasen er ikke s√• veldig viktig s√• den hopper jeg over her, men du kan finne den p√• github.

Det jeg har gjort derimot er √• lage wrapper funksjoner rundt metodene denne klassen tilbyr, s√• det blir enklere √• bytte den ut senere.

```fsharp
let getAllRecipes () = fakabase.GetRecipes ()
let addRecipe newRecipe =
    fakabase.AddRecipe newRecipe
let updateRecipe recipeToUpdate =
    fakabase.UpdateRecipe recipeToUpdate
let deleteRecipe id =
    fakabase.DeleteRecipe id
```

## HttpHandlers
Det aller siste vi trenger for √• f√• dette systemet til √• fungere er noen [HttpHandlers](https://github.com/giraffe-fsharp/Giraffe/blob/master/DOCUMENTATION.md#httphandler). Det er funksjonene som h√•ndterer http-requestene v√•re. Det vi vil at disse funksjonene skal gj√∏re er √• konvertere JSON som kommer med nettverkskallet over til oppskriftstypen vi allerede har definert. S√• skal de utf√∏re en oppdatering mot databasen v√•r og til slutt returnere noe.

```fsharp
let getRecipes: HttpHandler =
    fun (next: HttpFunc) (context: HttpContext) ->
        json (Recipe.getAllRecipes ()) next context

let postRecipe: HttpHandler =
    fun (next: HttpFunc) (context: HttpContext) ->
        task {
            let! newRecipe = context.BindJsonAsync<Recipe.Recipe>()
            Recipe.addRecipe newRecipe
            return! getRecipes next context
        }

let putRecipe: HttpHandler =
     fun (next: HttpFunc) (context: HttpContext) ->
        task {
            let! recipeToUpdate = context.BindJsonAsync<Recipe.Recipe>()
            Recipe.updateRecipe recipeToUpdate
            return! json recipeToUpdate next context
        }

let deleteRecipe (id: System.Guid): HttpHandler =
        Recipe.deleteRecipe id
        text $"Deleted recipe with id: {id}"
```

Noen ting √• legge merke til her:

- `next` er den neste http funksjonen som skal kj√∏res.
- `context` har informasjon om http-requesten.
- `task` er hvordan man kan bygge asynkrone kodeblokker, disse taskene oppf√∏rer seg likt som de gj√∏r i C#.
- Ut fra `context` kan vi mappe JSON bodyen over til oppskriftstypen vi har definert.
- Vi kan kalle andre HttpHandlere fra HttpHandlers, det kan vi se i `deleteRecipe` hvor vi kaller text handleren.

N√•r alt denne er inne, og koden forh√•pentligvis kompilerer, kan vi teste dette i rest-klient. Hos meg funker n√• alle disse rutene, her er GET:


![alt text](get.png "Resultat fra get request")

## Og vi er i m√•l¬†üèÅ
Da er vi ferdige. Det ble mye greier denne gangen s√• om du √∏nsker √• se alt i sin helhet er koden √• finne p√• github. Det eneste ekstra som finnes i repoet er CORS, en annen m√•te √• serializere F# typer til JSON p√• og v√•r fakabase.

Det vi har l√¶rt i dag er hvordan Giraffe fungerer og hvor enkelt det er √• sette opp en simpel backend med det. Vi har sett hvordan Giraffe er bygd p√• Kestrel og at konseptene er like med det vi kjenner fra C#. Vi kan n√• sette opp ruter og har f√•tt lagd en falsk database som bruker koden vi skrev forrige gang til √• forsyne oss med oppskrifter.

Jeg f√∏ler det er viktig √• nevne at det finnes mange alternativer til Giraffe. Selv liker jeg [Saturn](https://saturnframework.org/) godt. Det er et bibliotek som bygd p√• Giraffe og abstraherer bort en del av oppsettet. Det finnes andre alternativer ogs√•: [Oryx](https://github.com/cognitedata/oryx), [Suave](https://github.com/SuaveIO/suave), [Falco](https://github.com/pimbrouwers/Falco) og sikkert flere jeg ikke kommer p√• i farta. S√• her er det bare √• leke seg.

Selv om vi n√• kan dele oppskriftene v√•re med verden har serveren v√•r noen mangler:
- Som vi allerede vet har vi ingen database.¬†
- Ei heller har vi logging s√• om noen feil skulle inntreffe f√•r vi aldri beskjed.¬†
- Vi har ingen error h√•ndtering, s√• dersom systemet ikke f√•r til √• parset JSON bodyen over til en oppskrift g√•r det veldig galt.

Vi har med andre ord mye forbedringspotensiale her.

Neste gang skal vi lage en enkel frontend for dette systemet. Da skal vi l√¶re hvordan vi kan dele kode mellom frontend og backend, bruke F# til √• skrive react kode med typesikker markup og CSS. Det blir bra!